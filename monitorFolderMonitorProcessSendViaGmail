'TODO refactor to individual methods
'NOTE: storing passwords in scripts is not a safe practice

Dim objFSO, objStartFolder, objFolder
Dim colFiles, reportMessage
Dim emailtimestamp
Dim x, y
Dim errorFlag
Dim procName
Dim msg1, msg2
Dim service


'Folder to monitor for files
objStartFolder = "C:\Folder"
emailtimestamp = Now()
x = 0
y = 0
errorFlag = 0
'Process to monitor
'You can get this by going to task manager, right-clicking the process, and getting the properties
procName = "ProcessName.exe"
msg1 = ""
msg2 = ""

'Check to see if there are files in the folder and if folder exists
Set objFSO = CreateObject("Scripting.FileSystemObject")
If (objFSO.FolderExists(objStartFolder)) Then
	Set objFolder = objFSO.GetFolder(objStartFolder)
	Set colFiles = objFolder.Files
	For Each objFile in colFiles
		x = x + 1
	Next
	If x > 0 Then
		msg1 = x & " files in " & objStartFolder
		errorFlag = 1
	End If
Else
	msg1 = objStartFolder & " doesn't exist."
	errorFlag = 2
End If


'Check to see if processes are running
set service = GetObject ("winmgmts:")

for each Process in Service.InstancesOf ("Win32_Process")
	If Process.Name = procName then	
		y = y + 1
	End If
next

If NOT (y = 6) Then
	errorFlag = 3
	msg2 = y & " instance(s) of " & procName & " running."
End If

'Create the message
reportMessage = msg1 & chr(13) & chr(10) & msg2 & chr(13) & chr(10)

'Check to see if any errors occurred, then send an email from gmail
If errorFlag <> 0 Then
	'Email message
	Dim objMessage, strTo, strFrom, strSubject, strBody
	Const SMTPServer = "smtp.gmail.com"
	Const SMTPLogon = "your-email@gmail.com"
	Const SMTPPassword = "your-password"
	Const SMTPSSL = True
	Const SMTPPort = 465

	Const cdoSendUsingPickup = 1 	'Send message using local SMTP service pickup directory.
	Const cdoSendUsingPort = 2 	'Send the message using SMTP over TCP/IP networking.

	Const cdoAnonymous = 0 	' No authentication
	Const cdoBasic = 1 	' BASIC clear text authentication
	Const cdoNTLM = 2 	' NTLM, Microsoft proprietary authentication
	Set objMessage= CreateObject("CDO.Message")            
									
	strTo="to@some-email.com"
	strFrom="from@some-email.com"
	strSubject="Message Subject" & emailtimestamp
	strBody=reportMessage & " " & emailtimestamp

		objMessage.To = strTo
		objMessage.From = strFrom
		objMessage.Subject = strSubject
		objMessage.TextBody = strBody
		objMessage.Configuration.Fields.Item _
		("http://schemas.microsoft.com/cdo/configuration/sendusing") = 2	

		objMessage.Configuration.Fields.Item _
		("http://schemas.microsoft.com/cdo/configuration/smtpserver") = SMTPServer

		objMessage.Configuration.Fields.Item _
		("http://schemas.microsoft.com/cdo/configuration/smtpauthenticate") = cdoBasic

		objMessage.Configuration.Fields.Item _
		("http://schemas.microsoft.com/cdo/configuration/sendusername") = SMTPLogon

		objMessage.Configuration.Fields.Item _
		("http://schemas.microsoft.com/cdo/configuration/sendpassword") = SMTPPassword

		objMessage.Configuration.Fields.Item _
		("http://schemas.microsoft.com/cdo/configuration/smtpserverport") = SMTPPort

		objMessage.Configuration.Fields.Item _
		("http://schemas.microsoft.com/cdo/configuration/smtpusessl") = SMTPSSL

		objMessage.Configuration.Fields.Item _
		("http://schemas.microsoft.com/cdo/configuration/smtpconnectiontimeout") = 60
		
		objMessage.Configuration.Fields.Update
		objMessage.Send

	Set objMessage = Nothing
'msgbox "Email sent!"
End If


'msgbox "Done!"

